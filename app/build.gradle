apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'LsfPlanKey'
            storeFile file('I:/Henry/Dokumente/Pers√∂nlich/android_keys.jks')
            storePassword System.getenv("KSTOREPWD")
            keyPassword System.getenv("LSFKEYPWD")
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.hstrobel.lsfplan"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 27
        versionName "2.3"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    dexOptions {
        javaMaxHeapSize "6g"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        exclude('org/apache/commons/codec/language/bm/*.txt')		//  124 files (uncompressed 215kb) with unused language specific rules
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}


dependencies {
    implementation "com.android.support:appcompat-v7:${androidSupportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${androidSupportLibraryVersion}"
    implementation "com.android.support:support-v4:${androidSupportLibraryVersion}"
    implementation "com.android.support:design:${androidSupportLibraryVersion}"
    implementation "com.android.support:support-annotations:${androidSupportLibraryVersion}"

    implementation 'com.google.firebase:firebase-core:11.4.2'
    implementation 'com.google.firebase:firebase-ads:11.4.2'

    implementation ('org.mnode.ical4j:ical4j:1.0.6'){
        exclude group: 'commons-logging'
    }
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation('com.mikepenz:aboutlibraries:5.9.7@aar') {
        //support libarys are excluded due to the outdated versions used
        exclude group: 'com.android.support'
        transitive = true
    }
    //Keystore support lib
    implementation 'com.yakivmospan:scytale:1.0.1'

    androidTestImplementation "org.robolectric:robolectric:$robolectricVersion"
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation "com.android.support:support-annotations:${androidSupportLibraryVersion}"


    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    // testImplementation "org.robolectric:shadows-play-services:$robolectricVersion"
    testImplementation "org.robolectric:shadows-support-v4:$robolectricVersion"
    testImplementation 'org.mockito:mockito-core:1.10.19'


}


apply plugin: 'com.google.gms.google-services'